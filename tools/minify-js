#!/usr/bin/perl

$|++;

my @includes = grep s/^--include=//, @ARGV;

sub d {print STDERR ++$count, ': ', join(' ', @_) || '<empty>', "\n";
       wantarray ? @_ : $_[0]}

sub j {join "\n", @_}
sub h {map {$_ => 1} @_}

sub fix(&@) {my ($f, @xs) = @_; &$f(@xs) == @xs ? @xs : fix($f, &$f(@xs))}
sub uniq    {my %h; grep ! $h{$_}++, @_}

sub statics_in {map /[\w\$\.]+/go, @_}
sub methods_in {map /(\.[\w\$]+)/go, @_}
sub classes_in {map /new\h+([\w\.\$]+)/go, @_}
sub roots      {fix {uniq map /(([\w\$\.]+)(?:\.[\w\$]+))/go, @_} @_}

my @pieces = split /^([\$\w\.\[\]"]+)(\h*=\h*(?:function.*?\{(?:\V|\n\h)+\n^\}+$|\V*$))/mo, join '', <STDIN>;

# At this point, @pieces looks like this:
# '', 'foo.bar', ' = function () {\n...\n}', 'global = null', 'bif.baz', ' = function (x) {\n...\n}', ...
my %definitions = map @pieces[$_ * 3 + 1, $_ * 3 + 2], 0 .. $#pieces / 3;
my @non_defs    = map $pieces[$_ * 3], 0 .. $#pieces / 3;

my @instance    = grep   /\.prototype\./o, keys %definitions;
my @static      = grep ! /\.prototype\./o, keys %definitions;

my @live_set    = (undef);
my @copy        = ();

until (@live_set == @copy) {
  @copy = @live_set;

  my @code    = @non_defs, @definitions{@live_set};
  my @methods = grep s/^\./\\./, uniq methods_in @code;
  my @statics =                  uniq statics_in @code;
  my @classes =                  uniq classes_in @code;

  # Grab all instance methods that might be mentioned here.
  my @resolved_methods = uniq map {my $r = $_; grep(/$r$/, @instance)} @methods;

  @live_set = uniq roots @includes, @live_set, @statics, @classes, @resolved_methods;
}

my %live_set = h @live_set;

print join '', map @pieces[$_ * 3, $live_set{$pieces[$_ * 3 + 1]} ? ($_ * 3 + 1, $_ * 3 + 2) : ()], 0 .. $#pieces / 3;

__END__

=head1 SYNOPSYS

minify-js file1 [file2 [... fileN]] > output.js

=head1 DESCRIPTION

This program minifies HaXe-generated JavaScript. It removes unused classes, static methods, and instance methods, though because of the dynamically-typed nature of JavaScript it doesn't
necessarily catch everything. Note that this script doesn't work for JS minification in the general case. It relies on HaXe-formatted output, which is of a very specific form. Each definition
occurs on its own line, and indentation is consistent enough to reflect the structure of the generated code. Neither of these is true for JavaScript in general.

=cut
