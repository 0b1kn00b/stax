class CallbacksE{
  @:noUsing
  static public function create<A,B>():CallbackE<A,B>{
    return function(a:Either<A,B>->Void){ }; 
  }
  static public function receiveOf<R,A,B>(fn:(A -> B -> Void) -> Void):RC<Void,Either<A,B>>{
    return 
      function(fn0:Either<A,B>->Void):Void{
        fn(
          function(a,b){
            if(a!=null){
              fn0(Left(a));
            }else{
              fn0(Right(b));
            }
          }
        );
      }
  }
  static public function onSuccess<A,B>(ft:Callback<Either<A,B>>,fn:B->Void){
    return ft.eachR(fn);
  }
  static public function onFail<A,B>(ft:Callback<Either<A,B>>,fn:A->Void){
    return ft.eachL(fn);
  }
  /**
   Does a map if the Either is Left.
  */
  static public function mapLeft<A,B,C>(f:Callback<Either<A,B>>,fn:A->C):Callback<Either<C,B>>{
    return 
      f.map(
        function(x:Either<A,B>):Either<C,B>{
          return 
            x.mapLeft(
              function(y:A){
                return fn(y);
              }
            );
        }
      );
  }
  /**
   Does a map if the Either is Right.
  */
  static public function mapRight<A,B,C>(f:Callback<Either<A,B>>,fn:B->C):Callback<Either<A,C>>{
    return 
      f.map(
        function(x:Either<A,B>):Either<A,C>{
          return 
            x.mapRight(
              function(y:B){
                return fn(y);
              }
            );
        }
      );
  }
  /**
    Zips the right hand value with function `fn`
  */
  static public function zipRWith<A,B,C,D>(f0:Callback<Either<A,B>>,f1:Callback<Either<A,C>>,fn : B -> C -> D):Callback<Either<A,D>>{
    return 
      f0.zipWith(f1,
        function(a,b){
          return 
            switch (a) {
              case Left(v1)       : Left(v1);
              case Right(v1)      :
                switch (b) {
                  case Left(v2)   : Left(v2);
                  case Right(v2)  : Right(fn(v1,v2));
                }
              }
          }
      );
  }
  /**
    Zips the right hand value.
  */
  static public function zipR<A,B,C>(f0:Callback<Either<A,B>>,f1:Callback<Either<A,C>>):Callback<Either<A,Tuple2<B,C>>>{
    return zipRWith(f0,f1,tuple2);
  }
  /**
    flatMaps the right hand value
  */
  static public function flatMapRight<A,B,C>(f0:Callback<Either<A,B>>,fn : B -> Callback<Either<A,C>>):Callback<Either<A,C>>{
    return
      f0.flatMap(
        function(x){
          return
            switch (x) {
              case Left(v1)   : Callbacks.pure(Left(v1));
              case Right(v2)  : fn(v2);
            }
        }
      );
  }
  /**
    Flatmaps the left hand value.
  */
  static public function flatMapLeft<A,B,C>(f0:Callback<Either<A,B>>,fn : A -> Callback<Either<C,B>>):Callback<Either<C,B>>{
    return
      f0.flatMap(
        function(x){
          return
            switch (x) {
              case Right(v1)   : Callbacks.pure(Right(v1));
              case Left(v2)    : fn(v2);
            }
        }
      );
  }
  @:noUsing
  static public function pure<A,B>(e:Either<A,B>):Callback<Either<A,B>>{
    return cast Continuation.pure(e);
  }
  /**
    Resolves as a right hand value.
  */
  static public function right<A,B>(f:Callback<Either<A,B>>,v:B->Void):Void{
    f.map( Eithers.right ).each( Options.each.p2(v).enclose() );
  }
  /**
    Resolves as a left hand value.
  */
  static public function left<A,B>(f:Callback<Either<A,B>>,v:A->Void):Void{
    f.map( Eithers.left ).each( Options.each.p2(v).enclose() );
  }
  /**
    Creates a pure Callback and delivers to the right hand side.
  */
  static public function success<A,B>(v:B):Callback<Either<A,B>>{
    return pure(Right(v));
  }
  /**
    Creates a pure future and delivers to the left hand side.
  */
  static public function failure<A,B>(v:A):Callback<Either<A,B>>{
    return pure(Left(v));
  }
  /**
    Use this with a flatmap fold to wait for parallel futures.
    vals.map( function_returning_future ).foldLeft( Callback.pure(Right([])), Promises.waitfold )
    This op stops when there is a single failure
  */
  static public function waitfold<A,B>(init:Callback<Either<A,Array<B>>>,ft:Callback<Either<A,B>>):Callback<Either<A,Array<B>>>{
    return 
      init.flatMapRight(
        function(arr:Array<B>){
          return 
            ft.mapRight(
              function(v:B):Array<B>{
                return arr.add(v);
              }
            );
        }
      );
  }
  /**
    Returns a single future with an Array of the results, or an Fail.
  */
  static public function wait<A,B>(a:Array<Callback<Either<A,B>>>):Callback<Either<A,Array<B>>>{
    return 
      a.foldLeft(
          CallbacksE.pure(Right([]))
        , waitfold
      );
  }
  /**
    Applies a function if the result is right
  */
  static public function eachR<A,B>(v:Callback<Either<A,B>>,f:B->Void){
    return 
      v.each(
        Eithers.right.then( Options.each.p2( f ) ).enclose()
      );
  }
  /**
    Applies a function if the result is left.
  */
  static public function eachL<A,B>(v:Callback<Either<A,B>>,f:A->Void){
    return 
      v.each(
        Eithers.left.then( Options.each.p2( f ) ).enclose()
      );
  }
  static public function unzip<A,B,C>(tp:Tuple2<Callback<Either<A,B>>,Callback<Either<A,C>>>):Callback<Either<A,Tuple2<B,C>>>{
    return 
      tp.fst().flatMapRight(
        function(b:B){
          return tp.snd().mapRight( tuple2.p1(b) );
        }
      );
  }
  static public function bindFoldR<A,B,Err>(iter:Iterable<A>,start:Callback<Either<Err,B>>,fm:B->A->Callback<Either<Err,B>>):Callback<Either<Err,B>>{
    return 
      Callbacks.bindFold(
        iter,
        start,
        function(memo:Either<Err,B>,next:A){
          return 
              switch (memo) {
                case Left(e)    : Callbacks.pure(Left(e));
                case Right(v1)  : fm(v1,next);
              }
        }
      );
  }
}
class CallbacksEActions {
  public static function chain(a:Array<Void->Callback<Either<Fail,Dynamic>>>):Callback<Outcome<Array<Dynamic>>>{
    return 
      a.foldLeft(
        Callbacks.pure(Right([]))
      , function(init,fn){
          return 
            init.flatMapRight(function(x) return CallbacksE.mapRight(fn(),function(y) return x.add(y)));
        }
    );
  }
}