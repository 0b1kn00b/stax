class CallbacksE{
  @:noUsing
  static public function create<A,B>():CallbackE<A,B>{
    return function(a:Either<A,B>->Void){ }; 
  }
  static public function receiveOf<R,A,B>(fn:(A -> B -> Void) -> Void):RC<Void,Either<A,B>>{
    return 
      function(fn0:Either<A,B>->Void):Void{
        fn(
          function(a,b){
            if(a!=null){
              fn0(Left(a));
            }else{
              fn0(Right(b));
            }
          }
        );
      }
  }
  static public function onSuccess<A,B>(ft:Callback<Either<A,B>>,fn:B->Void){
    return ft.foreachR(fn);
  }
  static public function onFailure<A,B>(ft:Callback<Either<A,B>>,fn:A->Void){
    return ft.foreachL(fn);
  }
  /**
   Does a map if the Either is Left.
  */
  static public function mapL<A,B,C>(f:Callback<Either<A,B>>,fn:A->C):Callback<Either<C,B>>{
    return 
      f.map(
        function(x:Either<A,B>):Either<C,B>{
          return 
            x.mapL(
              function(y:A){
                return fn(y);
              }
            );
        }
      );
  }
  /**
   Does a map if the Either is Right.
  */
  static public function mapR<A,B,C>(f:Callback<Either<A,B>>,fn:B->C):Callback<Either<A,C>>{
    return 
      f.map(
        function(x:Either<A,B>):Either<A,C>{
          return 
            x.mapR(
              function(y:B){
                return fn(y);
              }
            );
        }
      );
  }
  /**
    Zips the right hand value with function `fn`
  */
  static public function zipRWith<A,B,C,D>(f0:Callback<Either<A,B>>,f1:Callback<Either<A,C>>,fn : B -> C -> D):Callback<Either<A,D>>{
    return 
      f0.zipWith(f1,
        function(a,b){
          return 
            switch (a) {
              case Left(v1)       : Left(v1);
              case Right(v1)      :
                switch (b) {
                  case Left(v2)   : Left(v2);
                  case Right(v2)  : Right(fn(v1,v2));
                }
              }
          }
      );
  }
  /**
    Zips the right hand value.
  */
  static public function zipR<A,B,C>(f0:Callback<Either<A,B>>,f1:Callback<Either<A,C>>):Callback<Either<A,Tuple2<B,C>>>{
    return zipRWith(f0,f1,tuple2);
  }
  /**
    flatMaps the right hand value
  */
  static public function flatMapR<A,B,C>(f0:Callback<Either<A,B>>,fn : B -> Callback<Either<A,C>>):Callback<Either<A,C>>{
    return
      f0.flatMap(
        function(x){
          return
            switch (x) {
              case Left(v1)   : Callbacks.pure(Left(v1));
              case Right(v2)  : fn(v2);
            }
        }
      );
  }
  /**
    Flatmaps the left hand value.
  */
  static public function flatMapL<A,B,C>(f0:Callback<Either<A,B>>,fn : A -> Callback<Either<C,B>>):Callback<Either<C,B>>{
    return
      f0.flatMap(
        function(x){
          return
            switch (x) {
              case Right(v1)   : Callbacks.pure(Right(v1));
              case Left(v2)    : fn(v2);
            }
        }
      );
  }
  @:noUsing
  static public function pure<A,B>(e:Either<A,B>):Callback<Either<A,B>>{
    return cast Continuation.pure(e);
  }
  /**
    Resolves as a right hand value.
  */
  static public function right<A,B>(f:Callback<Either<A,B>>,v:B->Void):Void{
    f.map( Eithers.right ).foreach( Options.foreach.p2(v).effectOf() );
  }
  /**
    Resolves as a left hand value.
  */
  static public function left<A,B>(f:Callback<Either<A,B>>,v:A->Void):Void{
    f.map( Eithers.left ).foreach( Options.foreach.p2(v).effectOf() );
  }
  /**
    Creates a pure Callback and delivers to the right hand side.
  */
  static public function success<A,B>(v:B):Callback<Either<A,B>>{
    return pure(Right(v));
  }
  /**
    Creates a pure future and delivers to the left hand side.
  */
  static public function failure<A,B>(v:A):Callback<Either<A,B>>{
    return pure(Left(v));
  }
  /**
    Use this with a flatmap fold to wait for parallel futures.
    vals.map( function_returning_future ).foldl( Callback.pure(Right([])), Promises.waitfold )
    This op stops when there is a single failure
  */
  static public function waitfold<A,B>(init:Callback<Either<A,Array<B>>>,ft:Callback<Either<A,B>>):Callback<Either<A,Array<B>>>{
    return 
      init.flatMapR(
        function(arr:Array<B>){
          return 
            ft.mapR(
              function(v:B):Array<B>{
                return arr.add(v);
              }
            );
        }
      );
  }
  /**
    Returns a single future with an Array of the results, or an Error.
  */
  static public function wait<A,B>(a:Array<Callback<Either<A,B>>>):Callback<Either<A,Array<B>>>{
    return 
      a.foldl(
          CallbacksE.pure(Right([]))
        , waitfold
      );
  }
  /**
    Applies a function if the result is right
  */
  static public function foreachR<A,B>(v:Callback<Either<A,B>>,f:B->Void){
    return 
      v.foreach(
        Eithers.right.then( Options.foreach.p2( f ) ).effectOf()
      );
  }
  /**
    Applies a function if the result is left.
  */
  static public function foreachL<A,B>(v:Callback<Either<A,B>>,f:A->Void){
    return 
      v.foreach(
        Eithers.left.then( Options.foreach.p2( f ) ).effectOf()
      );
  }
  static public function unzip<A,B,C>(tp:Tuple2<Callback<Either<A,B>>,Callback<Either<A,C>>>):Callback<Either<A,Tuple2<B,C>>>{
    return 
      tp.fst().flatMapR(
        function(b:B){
          return tp.snd().mapR( tuple2.p1(b) );
        }
      );
  }
  static public function bindFoldR<A,B,Err>(iter:Iterable<A>,start:Callback<Either<Err,B>>,fm:B->A->Callback<Either<Err,B>>):Callback<Either<Err,B>>{
    return 
      Callbacks.bindFold(
        iter,
        start,
        function(memo:Either<Err,B>,next:A){
          return 
              switch (memo) {
                case Left(e)    : Callbacks.pure(Left(e));
                case Right(v1)  : fm(v1,next);
              }
        }
      );
  }
}
class CallbacksEActions {
  public static function chain(a:Array<Void->Callback<Either<Error,Dynamic>>>):Callback<Outcome<Array<Dynamic>>>{
    return 
      a.foldl(
        Callbacks.pure(Right([]))
      , function(init,fn){
          return 
            init.flatMapR(function(x) return CallbacksE.mapR(fn(),function(y) return x.add(y)));
        }
    );
  }
}